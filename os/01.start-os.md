## Hello World实例

```
		ORG		0x7c00
		
; 标准FAT12格式软盘专用代码
		
		JMP		entry
		DB 		0x90
		DB		"HELLOIPL"		; 启动区的名称（8字节）
		DW		512				; 每个扇区的大小
		DB		1				; 簇(cluster)大小(必须为1个扇区)
		DW		1				; FAT 的起始位置（一般从第一个扇区开始）
		DB		2				; FAT 的个数
		DW		224				; 根目录大小
		DW		2880			; 磁盘大小
		DB		0xf0			; 磁盘种类
		DW		9				; FAT 的长度
		DW		18				; 一个磁道有几个扇区
		DW		2				; 磁头数
		DD		0				; 不使用分区
		DD		2880			; 重写一次磁盘大小
		DB		0, 0, 0x29		; 意义不明，固定
		DD		0xffffffff
		DB		"HELLO-OS   "	; 磁盘名称(11字节)
		DB		"FAT12   "		; 磁盘格式名称(8字节)
		RESB 	18				; 先空出18字节
		
		
	
entry:
		MOV		AX, 0
		MOV		SS, AX
		MOV		SP, 0x7c00
		MOV		DS, AX
		MOV		ES, AX
		
		MOV		SI, msg
		
putloop:
		MOV		AL, [SI]
		ADD		SI, 1
		CMP		AL, 0
		
		JE		fin
		MOV		AH, 0x0e
		MOV		BX, 15
		INT		0x10
		JMP		putloop

fin:
		HLT
		JMP		fin
		
msg:
		DB		0x0a, 0x0a
		DB		"hello, world"
		DB		0x0a
		DB		0
		
		
		RESB	0x7dfe-$
		DB		0x55, 0xaa
```

## 启动区 (boot sector)

软盘的第一个扇区成为启动区。一个扇区512个字节， 最后2个字节为55 AA

启动区装载地址 0x00007c00-0x00007dff

## 显示字符(BIOS中断)   INT 10H

1. 0EH功能

	功能描述： 在Teletype模式下显示字符

	入口参数: 
		
		AH = 0EH
		
		AL = 字符

		BH = 页码

		BL = 前景色(图形模式)

	出口参数：
	
		无


2. 功能06H和07H 

	功能描述：初始化屏幕或滚屏

	入口参数：

	AH＝06H——向上滚屏，07H——向下滚屏 

	AL＝滚动行数(0——清窗口) 

	BH＝空白区域的缺省属性                      

	(CH、CL)＝窗口的左上角位置(Y坐标，X坐标)    

	(DH、DL)＝窗口的右下角位置(Y坐标，X坐标)

	出口参数：无




3. 13H功能

	功能描述：在Teletype模式下显示字符串

	入口参数：

		AH＝13H

		BH＝页码 

		BL＝属性(若AL=00H或01H)

		CX＝显示字符串长度       

		(DH、DL)＝坐标(行、列)    

		ES:BP＝显示字符串的地址 

		AL＝显示输出方式

		0——字符串中只含显示字符，其显示属性在BL中。显示后，光标位置不变

		1——字符串中只含显示字符，其显示属性在BL中。显示后，光标位置改变

		2——字符串中含显示字符和显示属性。显示后，光标位置不变

		3——字符串中含显示字符和显示属性。显示后，光标位置改变
			
		出口参数：无


	所谓属性是指字符的颜色、背景颜色、是否闪烁、有没有底线等性质。在彩色显示卡 (CGA/EGA/VGA等)的文字模式中，颜色是用4个位表示，故可以表现出16种颜色，如下表：

	|二进制数|颜色|二进制数|颜色|
	|---|---|---|---|--|
	|0000|黑色（black）|1000|灰色（gray)|
	|0001|蓝色（blue）|1001|淡蓝色（light blue）|
	|0010|绿色（green）|1010|淡绿色（light green）|
	|0011|青色（cyan）|1011|淡青色（light cyan）|
	|0100|红色（red）|1100|淡红色（light red）|
	|0101|紫红色（magenta）|1101|淡紫红色（light magenta）|
	|0110|棕色（brown）|1110|黄色（yellow）|
	|0111|银色（light gray）|1111|白色（white）|

	在彩色显示器里，如CGA、EGA、VGA等，常用一个字节(8个位)来表示文字颜色和背景颜色，通常以第0～3位表示文字本身颜色；第4～6位表示背景颜色，背景颜色只有上表左栏的8种而已；第7个位，表示是否闪烁，0表示不闪烁，1表示闪烁。

	但是在单色显示器里，如MDA和Hercules卡中，这些颜色表并无意义，所以属性解释方式不同，请看下表：

	|数值|属性|
	|---|---|
	|00H|空格，不显示任何数据|
	|77H|显示白色方块|
	|07H|正常的黑底白字|
	|70H|反白的白底黑子|
	|01H|加底线|




## 直接磁盘服务(Direct Disk Service——INT 13H)

* 00H —磁盘系统复位0EH —读扇区缓冲区
* 01H —读取磁盘系统状态0FH —写扇区缓冲区
* 02H —读扇区10H —读取驱动器状态
* 03H —写扇区11H —校准驱动器
* 04H —检验扇区12H —控制器RAM诊断
* 05H —格式化磁道13H —控制器驱动诊断
* 06H —格式化坏磁道14H —控制器内部诊断
* 07H —格式化驱动器15H —读取磁盘类型
* 08H —读取驱动器参数16H —读取磁盘变化状态
* 09H —初始化硬盘参数17H —设置磁盘类型
* 0AH —读长扇区18H —设置格式化媒体类型
* 0BH —写长扇区19H —磁头保护
* 0CH —查寻1AH —格式化ESDI驱动器
* 0DH —硬盘系统复位

1. 功能00H

   功能描述：磁盘系统复位
   
   入口参数：
   
   AH＝00H 
   
   DL＝驱动器，00H~7FH：软盘；80H~0FFH：硬盘

   出口参数：
   
   CF＝0——操作成功，AH＝00H，否则，AH＝状态代码，参见功能号01H中的

2. 功能02H

   功能描述：读扇区

   入口参数：

   AH＝02H
   
   AL＝扇区数

   CH＝柱面

   CL＝扇区

   DH＝磁头	

   DL＝驱动器，00H~7FH：软盘；80H~0FFH：硬盘

   ES:BX＝缓冲区的地址

   出口参数：
   
   CF＝0——操作成功，AH＝00H，AL＝传输的扇区数，否则，AH＝状态代码


